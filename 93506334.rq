SELECT ?line WITH {
SELECT (MAX(?parent) AS ?star) (MAX(?number) AS ?num) (MAX(?parentLabel) AS ?label) (MAX(?dist) AS ?ly) (MAX(?lum) AS ?l) (MAX(?mass) AS ?m) (MAX(?ru_wiki) AS ?ru) 
       (MAX(?code) AS ?dr2) (MAX(?per) AS ?p) (MAX(?semi) AS ?s) (MAX(?ecc) AS ?e) (MAX(?exoplanet_eu) AS ?eu) (GROUP_CONCAT(DISTINCT ?sss; SEPARATOR=" ") AS ?src) {
  SELECT ?parent ?parentLabel ?host ?ru_wiki ?exo ?number ?dist ?lum ?per ?semi ?ecc ?mass ?sss ?exoplanet_eu ?code {
    {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?designation; wikibase:rank wikibase:NormalRank]; wdt:P361? ?parent
      OPTIONAL {?parent p:P527[ps:P527 ?host; pq:P1545 ?component]}
      BIND(IF(BOUND(?component), CONCAT(?component, ?designation), ?designation) AS ?number)
      VALUES ?types { wd:Q523 wd:Q595871 wd:Q101600 }
      ?parent wdt:P31/wdt:P279* ?types
      OPTIONAL { [] schema:about ?parent; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru_wiki }
      FILTER NOT EXISTS { ?parent ^ps:P527/pq:P1545 [] }
      OPTIONAL { ?dist_statement ^p:P2583 ?host; wikibase:rank wikibase:NormalRank; psv:P2583/wikibase:quantityAmount ?dist_amount }
      OPTIONAL { ?dist_statement ^p:P2583 ?host; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }  
      BIND (ROUND(?dist_amount*3.26156) AS ?dist)          
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank]
      OPTIONAL { ?host p:P528[ps:P528 ?code; pq:P972 wd:Q51905050] }
      OPTIONAL { ?lum_statement ^p:P2060 ?host; wikibase:rank wikibase:NormalRank; psv:P2060/wikibase:quantityAmount ?lum_amount }
      OPTIONAL { ?lum_statement ^p:P2060 ?host; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(REPLACE(STR(ROUND(1000*?lum_amount)/1000), "\\.", ",") AS ?lum)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank]
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; psv:P2146/wikibase:quantityAmount ?per_amount }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; psv:P2146/wikibase:quantityUpperBound ?per_upper }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; psv:P2146/wikibase:quantityUnit ?per_unit }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(IF(?per_unit = wd:Q573, 100, IF(?per_unit = wd:Q577, 36525, "NaN"^^xsd:float)) AS ?per_mult)
      BIND(IF(BOUND(?per_upper), ROUND(?per_mult*(?per_upper - ?per_amount))/100, 0) AS ?per_error)
      BIND(REPLACE(IF(?per_error > 0, CONCAT(STR(ROUND(?per_mult * ?per_amount)/100), " ± ", STR(?per_error)), STR(ROUND(?per_mult * ?per_amount)/100)), "\\.", ",") AS ?per)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank]
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; psv:P2233/wikibase:quantityAmount ?semi_amount }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; psv:P2233/wikibase:quantityUpperBound ?semi_upper }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; psv:P2233/wikibase:quantityUnit ?semi_unit }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(IF(?semi_unit = wd:Q1811, 100, "NaN"^^xsd:float) AS ?semi_mult)
      BIND(IF(BOUND(?semi_upper), ROUND(?semi_mult*(?semi_upper - ?semi_amount))/100, 0) AS ?semi_error)
      BIND(REPLACE(IF(?semi_error > 0, CONCAT(STR(ROUND(?semi_mult * ?semi_amount)/100), " ± ", STR(?semi_error)), STR(ROUND(?semi_mult * ?semi_amount)/100)), "\\.", ",") AS ?semi)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank]
      OPTIONAL { ?ecc_statement ^p:P1096 ?exo; wikibase:rank wikibase:NormalRank; psv:P1096/wikibase:quantityAmount ?ecc_amount }
      OPTIONAL { ?ecc_statement ^p:P1096 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(REPLACE(STR(ROUND(100*?ecc_amount)/100), "\\.", ",") AS ?ecc)    
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank]
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; psv:P2051/wikibase:quantityAmount ?msini_amount }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; psv:P2051/wikibase:quantityUpperBound ?msini_upper }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; psv:P2051/wikibase:quantityUnit ?msini_unit }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(IF(?msini_unit = wd:Q651336, 1000, IF(?msini_unit = wd:Q681996, 3.1780, "NaN"^^xsd:float)) AS ?msini_mult)
      BIND(IF(BOUND(?msini_upper), ROUND(?msini_mult*(?msini_upper - ?msini_amount))/1000, 0) AS ?msini_error)
      BIND(REPLACE(IF(?msini_error > 0, CONCAT(STR(ROUND(?msini_mult * ?msini_amount)/1000), " ± ", STR(?msini_error)), STR(ROUND(?msini_mult * ?msini_amount)/1000)), "\\.", ",") AS ?msini)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 []; wikibase:rank wikibase:NormalRank].
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; psv:P2067/wikibase:quantityAmount ?m_amount }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; psv:P2067/wikibase:quantityUpperBound ?m_upper }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; psv:P2067/wikibase:quantityUnit ?m_unit }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(IF(?m_unit = wd:Q651336, 1000, IF(?m_unit = wd:Q681996, 3.1780, "NaN"^^xsd:float)) AS ?m_mult)
      BIND(IF(BOUND(?m_upper), ROUND(?m_mult*(?m_upper - ?m_amount))/1000, 0) AS ?m_error)
      BIND(REPLACE(IF(?m_error > 0, CONCAT(STR(ROUND(?m_mult * ?m_amount)/1000), " ± ", STR(?m_error)), STR(ROUND(?m_mult * ?m_amount)/1000)), "\\.", ",") AS ?m)
    }
    OPTIONAL { ?exo wdt:P5653 ?exoplanet_eu }
    BIND(REPLACE(REPLACE(REPLACE(STR(?source), "http://www.wikidata.org/entity/", ""), "P", "Q"), "(Q1385430|Q51905050)", "") AS ?sss)
    BIND(IF(BOUND(?m), ?m, CONCAT("{{comment|>|масса экзопланеты, умноженная на угол наклонения}}", ?msini)) AS ?mass)
    SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en" }
  }
} GROUP BY ?host ?exo} AS %Q {
  {
    { SELECT (COUNT(*) AS ?exo_num) (COUNT(DISTINCT ?star) AS ?star_num) { INCLUDE %Q } }
    wd:{0} rdfs:label ?name; p:P360/pq:P59/^schema:about[schema:name ?ru; schema:isPartOf <https://ru.wikipedia.org/>]
    FILTER(LANG(?name) = "ru")
    BIND("A1" AS ?sortkey)
    BIND(?exo_num-ROUND(?exo_num/10-0.5)*10 AS ?digit)
    BIND(IF(?digit=1 && ?exo_num!=11, "у", IF(?digit IN (2,3,4) && (?exo_num-?digit)/10!=1, "ы", "")) AS ?suffix ) 
    BIND(CONCAT("'''{{subst:PAGENAME}}''' содержит ", STR(?exo_num), " [[экзопланета|экзопланет", ?suffix, "]] в ", STR(?star_num), 
                " разных [[Планетная система|планетных системах]], находящихся в созвездии [[", ?ru, "|", REPLACE(?name, "список экзопланет в созвездии ", "") , 
                "]] и отсортированных по возрастанию расстояния от Земли. ",
                "Учёные постоянно находят новые планеты вне [[Солнечная система|Солнечной системы]], поэтому список может быть неполон. ", 
                "Оценить [[Зона обитаемости|зону обитаемости]] можно на основе светимости звезды.") AS ?line)
  } UNION {
    VALUES (?sortkey ?line) { 
      ("A2" "")
      ("A3" "{| class='wikitable sortable' width=100%")
      ("A4" " |-  style='white-space: normal;'") 
      ("A5" " ! scope='col'; class='unsortable' | Система")
      ("A6" " ! scope='col'; class='unsortable' | {{comment|#|Планета}}")
      ("A7" " ! scope='col' | Удалённость<br>[[Световой год|св.&nbsp;лет]]")
      ("A8" " ! scope='col' | [[Светимость]]<br>{{Lo}}")
      ("A9" " ! scope='col' | [[Большая полуось]]<br>{{а.е.}}")
      ("AA" " ! scope='col' | [[Эксцентриситет (орбита)|Эксцентриситет]]")
      ("AB" " ! scope='col' | [[Сидерический период|Период обращения]]<br>[[сутки|сут]]")
      ("AC" " ! scope='col'; class='unsortable' | [[Масса]]<br>{{MJ}}")
      ("AD" " ! scope='col'; class='unsortable' | {{comment|Пр.|Примечания}}")
      ("C1" "|}")
      ("C2" "")
      ("C3" "== Примечания ==")
      ("C4" "{{примечания}}")
      ("C5" "")
      ("C8" "[[Категория:Википедия:Автоматически формируемые списки экзопланет по созвездиям]]")
    }
  } UNION {
    INCLUDE %Q
    BIND(CONCAT("B", IF(BOUND(?ly), SUBSTR(CONCAT("00000", STR(?ly)), STRLEN(STR(?ly))), CONCAT("99999999", ?label)), ?num) AS ?sortkey)
    BIND(CONCAT(" |-\n | ",IF(BOUND(?ru), CONCAT("[[", ?ru, IF(LCASE(?label) != LCASE(?ru), CONCAT("|", ?label), ""), "]]"), 
                        CONCAT("{{нет статьи|", REPLACE(STR(?star), "http://www.wikidata.org/entity/", ""), "|", ?label, "|Звезда}}")), " || ", 
                ?num, " || ", IF(BOUND(?ly), STR(?ly), ""), " || ", IF(BOUND(?l), ?l, ""), " || ", IF(BOUND(?s), ?s, ""), " || ", 
                IF(BOUND(?e), ?e, ""), " || ", IF(BOUND(?p), ?p, ""), " || ", IF(BOUND(?m), ?m, ""), " || ", 
                IF(BOUND(?eu), CONCAT("<sup>[http://exoplanet.eu/catalog/", ?eu, "/ EPE]</sup>"), ""), 
                IF(BOUND(?dr2), CONCAT("<sup>[http://vizier.u-strasbg.fr/viz-bin/VizieR-S?", ENCODE_FOR_URI(?dr2), " DR2]</sup>"), ""), 
                REPLACE(REPLACE(?src, "(Q\\d+)", "{{source-ref|$1}}"), " ", "")
               ) AS ?line)
  } UNION {
    wd:{0} p:P360/pq:P59/^schema:about[schema:name ?ru; schema:isPartOf <https://ru.wikipedia.org/>]
    BIND("C6" AS ?sortkey)
    BIND(CONCAT("[[Категория:Списки экзопланет|",?ru, "]]") AS ?line)
  } UNION {
    wd:{0} p:P360/pq:P59/wdt:P910/^schema:about[schema:name ?cat; schema:isPartOf <https://ru.wikipedia.org/>]
    BIND("C7" AS ?sortkey)
    BIND(CONCAT("[[", ?cat, "]]") AS ?line)
  }
} ORDER BY ?sortkey
