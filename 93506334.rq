SELECT ?line WITH {
SELECT (MAX(?parent) as ?star) ?num (MAX(?parentLabel) as ?label) (MAX(?dist) AS ?ly) (MAX(?lum) as ?l) (MAX(?mass) as ?m) (MAX(?ru_wiki) AS ?ru) 
       (MAX(?per) as ?p) (MAX(?semi) as ?s) (MAX(?ecc) as ?e) (MAX(?exoplanet_eu) AS ?eu) (GROUP_CONCAT(DISTINCT ?sss; SEPARATOR=" ") AS ?src) {
  SELECT ?parent ?parentLabel ?host ?ru_wiki ?exo ?num ?dist ?lum ?per ?semi ?ecc ?mass ?sss ?exoplanet_eu {
    {
      ?parent wdt:P59/^pq:P59/^p:P360 wd:{0}; wdt:P527?/p:P398[ps:P398 ?exo; pq:P1545 ?num].
      ?exo wdt:P397 ?host
      OPTIONAL { [] schema:about ?parent; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru_wiki }
      FILTER NOT EXISTS { ?parent ^ps:P527/pq:P1545 [] }
      OPTIONAL { ?dist_statement ^p:P2583 ?host; wikibase:rank wikibase:NormalRank; psv:P2583/wikibase:quantityAmount ?dist_amount }
      OPTIONAL { ?dist_statement ^p:P2583 ?host; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }  
      BIND (ROUND(?dist_amount*3.26156) AS ?dist)          
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?num]
      OPTIONAL { ?lum_statement ^p:P2060 ?host; wikibase:rank wikibase:NormalRank; psv:P2060/wikibase:quantityAmount ?lum_amount }
      OPTIONAL { ?lum_statement ^p:P2060 ?host; wikibase:rank wikibase:NormalRank; psv:P2060/wikibase:quantityUpperBound ?lum_upper }
      OPTIONAL { ?lum_statement ^p:P2060 ?host; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }  
      BIND(REPLACE(CONCAT(STR(?lum_amount), IF(BOUND(?lum_upper), CONCAT(" ± ", STR(?lum_upper - ?lum_amount)), "")), "\\.", ",") AS ?lum)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?num]
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; psv:P2146/wikibase:quantityAmount ?per_amount }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; psv:P2146/wikibase:quantityUpperBound ?per_upper }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      OPTIONAL { ?per_statement ^p:P2146 ?exo; wikibase:rank wikibase:NormalRank; 
                               psv:P2146/wikibase:quantityUnit [^schema:about [schema:name ?per_unit; schema:isPartOf <https://ru.wikipedia.org/>]; wdt:P5061 ?per_sym] }
      FILTER (!BOUND(?per_sym) || LANG(?per_sym)="ru")
      BIND(CONCAT(REPLACE(IF(BOUND(?per_upper), CONCAT(STR(?per_amount), " ± ", STR(?per_upper - ?per_amount)), STR(?per_amount)), "\\.", ",")
                          , IF(BOUND(?per_sym), CONCAT(" [[", ?per_unit, "|", ?per_sym, "]]"), "")
                         ) AS ?per)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:Q21194221; p:P398[ps:P398 ?exo; pq:P1545 ?num]
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; psv:P2233/wikibase:quantityAmount ?semi_amount }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; psv:P2233/wikibase:quantityUpperBound ?semi_upper }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      OPTIONAL { ?semi_statement ^p:P2233 ?exo; wikibase:rank wikibase:NormalRank; 
                               psv:P2233/wikibase:quantityUnit [^schema:about [schema:name ?semi_unit; schema:isPartOf <https://ru.wikipedia.org/>]; wdt:P5061 ?semi_sym] }
      FILTER (!BOUND(?semi_sym) || LANG(?semi_sym)="ru")
      BIND(CONCAT(REPLACE(IF(BOUND(?semi_upper), CONCAT(STR(?semi_amount), " ± ", STR(?semi_upper - ?semi_amount)), STR(?semi_amount)), "\\.", ",")
                          , IF(BOUND(?semi_sym), CONCAT(" [[", ?semi_unit, "|", ?semi_sym, "]]"), "")
                         ) AS ?semi)
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?num]
      OPTIONAL { ?ecc_statement ^p:P1096 ?exo; wikibase:rank wikibase:NormalRank; psv:P1096/wikibase:quantityAmount ?ecc_amount }
      OPTIONAL { ?ecc_statement ^p:P1096 ?exo; wikibase:rank wikibase:NormalRank; psv:P1096/wikibase:quantityUpperBound ?ecc_upper }
      OPTIONAL { ?ecc_statement ^p:P1096 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      BIND(REPLACE(CONCAT(STR(?ecc_amount), IF(BOUND(?ecc_upper), CONCAT(" ± ", STR(?ecc_upper - ?ecc_amount)), "")), "\\.", ",") AS ?ecc)    
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?num]
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; psv:P2051/wikibase:quantityAmount ?msini_amount }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; psv:P2051/wikibase:quantityUpperBound ?msini_upper }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      OPTIONAL { ?msini_statement ^p:P2051 ?exo; wikibase:rank wikibase:NormalRank; 
                               psv:P2051/wikibase:quantityUnit [^schema:about [schema:name ?msini_unit; schema:isPartOf <https://ru.wikipedia.org/>]; wdt:P5061 ?msini_sym] }
      FILTER (!BOUND(?msini_sym) || LANG(?msini_sym)="ru")
      BIND(CONCAT(REPLACE(IF(BOUND(?msini_upper), CONCAT(STR(?msini_amount), " ± ", STR(?msini_upper - ?msini_amount)), STR(?msini_amount)), "\\.", ",")
                          , IF(BOUND(?msini_sym), CONCAT(" [[", ?msini_unit, "|", ?msini_sym, "]]"), "")
                         ) AS ?msini)    
    } UNION {
      ?host wdt:P59/^pq:P59/^p:P360 wd:{0}; p:P398[ps:P398 ?exo; pq:P1545 ?num].
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; psv:P2067/wikibase:quantityAmount ?m_amount }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; psv:P2067/wikibase:quantityUpperBound ?m_upper }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; prov:wasDerivedFrom/pr:P248 ?source }
      OPTIONAL { ?m_statement ^p:P2067 ?exo; wikibase:rank wikibase:NormalRank; 
                               psv:P2067/wikibase:quantityUnit [^schema:about [schema:name ?m_unit; schema:isPartOf <https://ru.wikipedia.org/>]; wdt:P5061 ?m_sym] }
      FILTER (!BOUND(?m_sym) || LANG(?m_sym)="ru")
      BIND(CONCAT(REPLACE(IF(BOUND(?m_upper), CONCAT(STR(?m_amount), " ± ", STR(?m_upper - ?m_amount)), STR(?m_amount)), "\\.", ",")
                          , IF(BOUND(?m_sym), CONCAT(" [[", ?m_unit, "|", ?m_sym, "]]"), "")
                         ) AS ?m)    
    }
    OPTIONAL { ?exo wdt:P5653 ?exoplanet_eu }
    BIND(REPLACE(REPLACE(REPLACE(STR(?source), "http://www.wikidata.org/entity/", ""), "P", "Q"), "Q1385430", "") AS ?sss)
    BIND(IF(BOUND(?m), ?m, CONCAT("{{comment|>|масса экзопланеты, умноженная на угол наклонения}}", ?msini)) AS ?mass)
    SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en" }
  }
} GROUP BY ?host ?num} AS %Q {
  {
    { SELECT (COUNT(*) AS ?exo_num) (COUNT(DISTINCT ?star) AS ?star_num) { INCLUDE %Q } }
    wd:{0} rdfs:label ?name; p:P360/pq:P59/^schema:about[schema:name ?ru; schema:isPartOf <https://ru.wikipedia.org/>]
    FILTER(LANG(?name) = "ru")
    BIND("A1" AS ?sortkey)
    BIND(CONCAT("'''{{subst:PAGENAME}}''' содержит ", STR(?exo_num), " [[экзопланета|экзопланет]] в ", STR(?star_num), 
                " разных [[Планетная система|планетных системах]], находящихся в созвездии [[", ?ru, "|", REPLACE(?name, "список экзопланет в созвездии ", "") , 
                "]] и отсортированных по возрастанию расстояния от Земли. ",
                "Учёные постоянно находят новые планеты вне [[Солнечная система|Солнечной системы]], поэтому список может быть неполон. ", 
                "Оценить [[Зона обитаемости|зону обитаемости]] можно на основе светимости звезды.") AS ?line)
  } UNION {
    VALUES (?sortkey ?line) { 
      ("A2" "")
      ("A3" "{| class='wikitable sortable'")
      ("A4" " |-  style='white-space: normal;'") 
      ("A5" " ! scope='col'; class='unsortable' | Система")
      ("A6" " ! scope='col'; class='unsortable' | {{comment|#|Планета}}")
      ("A7" " ! scope='col' | Удалённость ([[Световой год|св.&nbsp;лет]])")
      ("A8" " ! scope='col' | [[Масса]]")
      ("A9" " ! scope='col' | [[Светимость]] звезды ({{Lo}})")
      ("AA" " ! scope='col' | [[Большая полуось]]")
      ("AB" " ! scope='col' | [[Эксцентриситет (орбита)|Эксцентриситет]]")
      ("AC" " ! scope='col' | [[Сидерический период]]")
      ("AD" " ! scope='col'; class='unsortable'; style='text-align:left;' | Прим.")
      ("C1" "|}")
      ("C2" "")
      ("C3" "== Примечания ==")
      ("C4" "{{примечания}}")
      ("C5" "[[Категория:Списки экзопланет]]")
      ("C7" "[[Категория:Википедия:Автоматически формируемые списки экзопланет по созвездиям]]")
    }
  } UNION {
    INCLUDE %Q
    BIND(CONCAT("B", IF(BOUND(?ly), SUBSTR(CONCAT("00000", STR(?ly)), STRLEN(STR(?ly))), CONCAT("99999999", ?label)), ?num) AS ?sortkey)
    BIND(CONCAT(" |-\n | ",IF(BOUND(?ru), CONCAT("[[", ?ru, IF(LCASE(?label) != LCASE(?ru), CONCAT("|", ?label), ""), "]]"), 
                        CONCAT("{{нет статьи|", REPLACE(STR(?star), "http://www.wikidata.org/entity/", ""), "|", ?label, "|Звезда}}")), " || ", 
                ?num, " || ", IF(BOUND(?ly), STR(?ly), ""), " || ", IF(BOUND(?m), ?m, ""), " || ", IF(BOUND(?l), ?l, ""), " || ", IF(BOUND(?s), ?s, ""), " || ", 
                IF(BOUND(?e), ?e, ""), " || ", IF(BOUND(?p), ?p, ""), " || ", 
                IF(BOUND(?eu), CONCAT("[http://exoplanet.eu/catalog/", ?eu, "/ EPE]"), ""), REPLACE(?src, "(Q\\d+)", "{{source-ref|$1}}")
               ) AS ?line)
  } UNION {
    wd:{0} p:P360/pq:P59/wdt:P910/^schema:about[schema:name ?cat; schema:isPartOf <https://ru.wikipedia.org/>]
    BIND("C6" AS ?sortkey)
    BIND(CONCAT("[[", ?cat, "]]") AS ?line)
  }
} ORDER BY ?sortkey
