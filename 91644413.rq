SELECT ?line WITH { 
SELECT ?item (?itemLabel AS ?label) ?row {
  {
    wd:{0} wdt:P1423 ?constellation.
    ?item wdt:P59 ?constellation; p:P528[pq:P972 wd:Q105616; wikibase:rank wikibase:NormalRank; ps:P528 ?code].
    MINUS { ?item wdt:P31 wd:Q43001876 }
    MINUS { ?item wdt:P31 wd:Q11276; wdt:P59 ?constellation }
    ?constellation wdt:P1813 ?short
    BIND (REPLACE(?code, CONCAT(' ', STR(?short)), '') AS ?itemLabel)
    BIND (CONCAT('A', REPLACE(IF(REGEX(?itemLabel, '^[A-Za-z]'), CONCAT('ωω',?itemLabel), 
                                 IF(REGEX(?itemLabel, '\\s[A-Za-z][A-Za-z][A-Za-z]'), CONCAT('ωωω',?itemLabel), ?itemLabel)),'¹','1')) AS ?row)
  } UNION {
    wd:{0} wdt:P1423 ?constellation.
    ?item wdt:P59 ?constellation; p:P528[pq:P972 wd:Q111116; wikibase:rank wikibase:NormalRank; ps:P528 ?code].
    FILTER (?item NOT IN (wd:Q2469))
    ?constellation wdt:P1813 ?short
    BIND(REPLACE(?code, CONCAT(' ', STR(?short)), '') AS ?itemLabel)
    BIND(CONCAT('BB', IF(!CONTAINS(?itemLabel, ' '), SUBSTR(CONCAT('00', ?itemLabel), STRLEN(?itemLabel)), 
                         CONCAT('B', SUBSTR(CONCAT('00', ?itemLabel), STRLEN(STRBEFORE(?itemLabel, ' ')))))) AS ?row)
  } UNION {
    wd:{0} wdt:P1423 ?constellation.
    ?item wdt:P59 ?constellation; p:P528[pq:P972 wd:Q222662; wikibase:rank wikibase:NormalRank; ps:P528 ?code]; wikibase:sitelinks ?iw
    FILTER (?item NOT IN (wd:Q43933, wd:Q841138, wd:Q872204, wd:Q913140))
    FILTER NOT EXISTS { ?item wdt:P31 wd:Q3937 }
    ?constellation wdt:P1813 ?short
    BIND(REPLACE(?code, CONCAT(' ', STR(?short)), '') AS ?itemLabel)
    FILTER(REGEX(?itemLabel, '[A-Z].*') && (?iw > 0))
    BIND(CONCAT(IF(REGEX(?itemLabel, '^[R-Z]+$'), 'CC', 'CD'), STR(STRLEN(?itemLabel)), ?itemLabel) AS ?row)
  } UNION {
    SELECT DISTINCT ?item ?itemLabel {
      wd:{0} wdt:P1423 ?constellation.
      ?item wdt:P59 ?constellation; p:P398/pq:P1545 []
      SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en". }
    }
  } UNION {
    SELECT DISTINCT ?item ?itemLabel {
      wd:{0} wdt:P1423 ?constellation.
      ?item wdt:P59 ?constellation; p:P527[ps:P527/p:P398/pq:P1545 []; pq:P1545 []]
      SERVICE wikibase:label { bd:serviceParam wikibase:language "ru,en". }
    }
  }  
  FILTER NOT EXISTS { [] p:P527[ps:P527 ?item; pq:P1545 []] }
  BIND(IF(BOUND(?row), ?row, CONCAT('DD', IF(STRSTARTS(?itemLabel, 'COROT-'), CONCAT('COROT', SUBSTR(REPLACE(?itemLabel, 'COROT-', '00000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'HAT-P-'), CONCAT('HATP', SUBSTR(REPLACE(?itemLabel, 'HAT-P-', '00000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'HD '), CONCAT('HD', SUBSTR(REPLACE(?itemLabel, 'HD ', '00000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'K2-'), CONCAT('K2', SUBSTR(REPLACE(?itemLabel, 'K2-', '000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'Kepler-'), CONCAT('KE', SUBSTR(REPLACE(?itemLabel, 'Kepler-', '00000000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'KOI-'), CONCAT('KOI', SUBSTR(REPLACE(?itemLabel, 'KOI-', '00000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'TOI-'), CONCAT('TOI', SUBSTR(REPLACE(?itemLabel, 'TOI-', '00000000'), STRLEN(?itemLabel))), 
                                          IF(STRSTARTS(?itemLabel, 'WASP-'), CONCAT('WASP', SUBSTR(REPLACE(?itemLabel, 'WASP-', '00000000'), STRLEN(?itemLabel))), 
                                             UCASE(?itemLabel))))))))))) AS ?row)
}
} AS %Q {
  {
    VALUES (?row ?line) { 
      ('A1' '{{Навигационная таблица')
      ('A2' ' |имя           = {{subst:PAGENAME}}')
      ('A3' ' |state         = <includeonly>{{{state|autocollapse}}}</includeonly>')
      ('A5' ' |класс_списков = hlist hlist-items-nowrap')
      ('A6' '')
      ('A7' ' |заголовок1    = [[Обозначения Байера|Байер]]')
      ('A8' ' |список1       =')
      ('A9' '') 
      ('B1' '') 
      ('B2' ' |заголовок2    = [[Обозначения Флемстида|Флемстид]]') 
      ('B3' ' |список2       =') 
      ('B4' '')
      ('C1' '') 
      ('C2' ' |заголовок3    = [[Переменная звезда|Переменные]]') 
      ('C3' ' |список3       =') 
      ('C4' '')
      ('D1' '') 
      ('D2' ' |заголовок4    = [[Планетная система|Планетные<br>системы]]') 
      ('D3' ' |список4       =') 
      ('D4' '')
      ('E1' '') 
      ('E2' ' |заголовок5    = Другие') 
      ('E3' ' |список5       =') 
      ('E4' '')
      ('F1' '') 
      ('F3' ' }}<noinclude>')
      ('F4' '[[Категория:Навигационные шаблоны:Звёзды по созвездиям]]') 
      ('F5' '[[Категория:Википедия:Автоматически формируемые шаблоны по звёздам созвездий]]')
      ('F6' '</noinclude>')
    }  
  } UNION {
    BIND ('A4' as ?row)
    wd:{0} wdt:P1423 ?constellation.
    [] schema:about ?constellation; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ruconst.
    ?item p:P360[ps:P360 wd:Q523; pq:P59 ?constellation].
    [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?rulist
    BIND(CONCAT(' |заголовок     = Звёзды созвездия [[', ?ruconst, '|', REPLACE (?rulist, 'Список звёзд созвездия ', ''), ']]') AS ?line)
  } UNION {
    INCLUDE %Q
    OPTIONAL { [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru }
    BIND(IF(BOUND(?ru), CONCAT('* [[', ?ru, IF(LCASE(?label) != LCASE(?ru), CONCAT('|', ?label), ''), ']]'), 
                        CONCAT('* {{нет статьи|', REPLACE(STR(?item), 'http://www.wikidata.org/entity/', ''), '|', ?label, '|Звезда}}')) AS ?line)
  } UNION {
    { SELECT DISTINCT ?item ?ru {
        wd:{0} wdt:P1423 ?constellation.
        ?item wdt:P59 ?constellation.
        [] schema:about ?item; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?ru
        MINUS { INCLUDE %Q } # only those that were not listed above
        VALUES ?types { wd:Q595871 wd:Q523 wd:Q101600 }
        FILTER EXISTS { ?item wdt:P31/wdt:P279* ?types }
        MINUS { ?item wdt:P3208 []; wdt:P59 ?constellation }
        MINUS { ?item wdt:P31 wd:Q44559; wdt:P59 ?constellation }
        MINUS { ?item wdt:P31 wd:Q3937; wdt:P59 ?constellation }
        MINUS { ?item wdt:P31 wd:Q1049029; wdt:P59 ?constellation }
        MINUS { ?item wdt:P31 wd:Q4188; wdt:P59 ?constellation }
    } }
    BIND(CONCAT('* [[', ?ru, ']]') AS ?line)    
    BIND(CONCAT('EE', IF(STRSTARTS(?ru, 'HD '), CONCAT('HD', SUBSTR(REPLACE(?ru, 'HD ', '000000000'), STRLEN(?ru))), ?ru)) AS ?row)
  } UNION {
    wd:{0} wdt:P1423 ?constellation.
    BIND ('F2' as ?row)
    ?list p:P360[ps:P360 wd:Q523; pq:P59 ?constellation].
    [] schema:about ?list; schema:isPartOf <https://ru.wikipedia.org/>; schema:name ?rulist
    BIND(CONCAT(' | внизу        = [[', ?rulist, ']]') AS ?line)
  }
} ORDER BY ?row
